# $Id:
# Maintainer: Ivailo Monev <xakepa10@gmail.com>

# https://wiki.archlinux.org/index.php/Arch_package_guidelines

pkgname=katie-git
pkgver=4.11.1.r4681.8f22ea5f
pkgrel=1
pkgdesc='C++ toolkit derived from the Qt 4.8 framework'
arch=('i486' 'i686' 'pentium4' 'x86_64' 'arm')
url='https://github.com/fluxer/katie'
license=('LGPL' 'BSD')
depends=('libsm' 'libxcursor' 'libxinerama' 'icu' 'libcups' 'libxrandr' 'fontconfig'
         'xdg-utils' 'jansson')
makedepends=('cmake' 'git' 'unifdef')
source=("git+https://github.com/fluxer/katie")
sha1sums=('SKIP')
conflicts=('katie')
# options=('debug')

pkgver() {
    cd katie
    printf "4.11.1.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../katie \
        -Wno-dev \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKATIE_LDCONF_PATH=/etc/ld.so.conf.d \
        -DKATIE_PROFILE_PATH=/etc/profile.d \
        -DKATIE_TOOLS_SUFFIX="-katie"
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install

    install -vDm644 ../katie/LICENSE.BSD \
        "${pkgdir}/usr/share/licenses/katie-git/BSD.txt"
}
